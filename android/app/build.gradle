plugins {
  id("com.android.application")
  id("org.jetbrains.kotlin.android")
  id("com.facebook.react")
  id("com.google.gms.google-services")
}


def rnVersion = providers.gradleProperty("REACT_NATIVE_VERSION").orNull
if (!rnVersion) {
  def pkg = file("$rootDir/../node_modules/react-native/package.json")
  if (pkg.exists()) {
    def m = (pkg.text =~ /"version"\s*:\s*"([^"]+)"/)
    if (m.find()) rnVersion = m.group(1)
  }
}
if (!rnVersion) {
  rnVersion = "0.75.3" 
  logger.lifecycle("REACT_NATIVE_VERSION not provided; falling back to ${rnVersion}")
}

def rnAarVersion = rnVersion.replaceAll(/-.+$/, "")


react {
  autolinkLibrariesWithApp()
}

android {
  namespace "com.finguard"

  compileSdk 35
  buildToolsVersion "35.0.0"
  ndkVersion "27.1.12297006"

  defaultConfig {
    applicationId "com.finguard"
  
    minSdkVersion 24
    targetSdkVersion 35

    versionCode project.hasProperty("VERSION_CODE") ? project.VERSION_CODE.toInteger() : 1
    versionName project.hasProperty("VERSION_NAME") ? project.VERSION_NAME : "0.1.0"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = "17"
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }
    release {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
    }
  }

  packaging {
    resources {
      pickFirsts += ["META-INF/*"]
    }
  }
}

// Hermes on/off
def hermesEnabled = (project.findProperty("hermesEnabled") ?: "true").toBoolean()

dependencies {

  implementation("com.facebook.react:react-android:${rnAarVersion}")

  if (hermesEnabled) {
    implementation("com.facebook.react:hermes-android:${rnAarVersion}")
  } else {
    implementation("io.github.react-native-community:jsc-android:250230.2.1")
  }

  implementation("com.google.android.gms:play-services-base:18.5.0")
  implementation("com.google.android.gms:play-services-location:21.2.0")
  implementation("com.google.firebase:firebase-messaging:23.4.0")
}


configurations.all {
  resolutionStrategy {
    force "com.facebook.react:react-android:${rnAarVersion}"
    if (hermesEnabled) {
      force "com.facebook.react:hermes-android:${rnAarVersion}"
    }
  }
}
